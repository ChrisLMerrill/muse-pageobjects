apply plugin: 'java-library'
apply plugin: 'maven-publish'

repositories {
    mavenLocal()
    jcenter()
}

group 'org.museautomation'
version '0.4'

dependencies {
    compile 'org.museautomation:muse-core:[0.22,)'
    compile 'org.museautomation:muse-selenium:[0.22,)'

    testImplementation 'junit:junit:4+'
}

// jar the sources
task sourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

// jar the javadocs
task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

// produce the sources and javadoc artifacts
artifacts {
    archives sourcesJar
    archives javadocJar
}

// declare which packages Muse should scan for extensions
jar {
    manifest {
        attributes 'muse-package': 'org.museautomation.pageobject,org.museautomation.pageobject.steps'
    }
}

publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
                artifact sourcesJar
                artifact javadocJar
            }
        }
        repositories {
            mavenLocal()
        }
    }

wrapper {
    gradleVersion = '5.6.2'
}